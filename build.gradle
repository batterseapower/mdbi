buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
    }
}

apply plugin: 'java'

// Lets us sign our artifacts, construct a POM and use the uploadArchives task to upload to Sonatype OSS
// The disadvantage of going this route is that anything we upload to Sonatype has to be
// manually "closed" in the web interface (Nexus: https://oss.sonatype.org/) before it
// shows up on Maven Central.
apply plugin: 'com.bmuschko.nexus'

// So we augment Nexus with this plugin, which lets us upload the signed, POMified artifacts
// to Bintray. Bintray operate a Maven repo called JCenter which can be used as an alternative
// to Maven Central, but which also supports fully automatic syncing to Maven Central via Sonatype OSS.
// So this plugin lets us upload to Bintray + JCenter + Maven Central with a single "bintrayUpload" task
apply plugin: 'com.jfrog.bintray'

group = 'uk.co.omega-prime'
version = '2.0.0'

def projectName = 'mdbi'
def projectDescription = 'Max\'s DataBase Interface: a simple but powerful JDBC wrapper inspired by JDBI'

sourceCompatibility = 1.8

jar {
    baseName = projectName
    manifest {
        attributes 'Implementation-Title': projectName,
                   'Implementation-Version': version
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    testCompile group: 'org.xerial', name: 'sqlite-jdbc', version: '3.8.11.2'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// Used by the nexus plugin
modifyPom {
    project {
        name projectName
        description projectDescription
        url 'http://batterseapower.github.io/mdbi/'

        scm {
            url 'https://github.com/batterseapower/mdbi'
            connection 'scm:https://batterseapower@github.com/batterseapower/mdbi.git'
            developerConnection 'scm:git://github.com/batterseapower/mdbi.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'batterseapower'
                name 'Max Bolingbroke'
                email 'batterseapower@hotmail.com'
            }
        }
    }
}

// I tried to have the Nexus plugin sign things, rather than using the Bintray
// automatic signing support, but when e.g. mdbi-1.0.2-sources.jar.asc got uploaded
// to Bintray, Bintray seemed to mangle the filename to mdbi-1.0.2-sources-jar.asc.
// This would obviously cause the Maven Central publish to fail because it couldn't
// figure out what file that was meant to be a signature for. So I gave up, gave
// Bintray my GPG private key, and turned on auto-signing in the BinTray repo settings.
nexus {
    sign = false
}

// e.g. Travis won't have the Bintray config
if (hasProperty('bintrayUsername') || System.getenv().containsKey('BINTRAY_USER')) {
    // Used by the bintray plugin
    bintray {
        user = System.getenv().getOrDefault('BINTRAY_USER', bintrayUsername)
        key  = System.getenv().getOrDefault('BINTRAY_KEY', bintrayApiKey)
        publish = true

        pkg {
            repo = 'maven'
            name = projectName
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/batterseapower/mdbi.git'

            version {
                name = project.version
                desc = projectDescription
                released = new Date()

                mavenCentralSync {
                    user     = System.getenv().getOrDefault('SONATYPE_USER', nexusUsername)
                    password = System.getenv().getOrDefault('SONATYPE_PASSWORD', nexusPassword)
                }
            }
        }

        configurations = ['archives']
    }
}
